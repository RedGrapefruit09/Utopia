plugins {
    id 'fabric-loom' version '0.9.46'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.5.30'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        url = "https://maven.shedaniel.me"
    }
    maven {
        url = "https://raw.githubusercontent.com/RedGrapefruit09/Maven/master"
        content {
            includeGroup "com.redgrapefruit.itemnbt"
            includeGroup "com.redgrapefruit.redmenu"
        }
    }
    maven {
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    include "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Fabric Language Kotlin. Provides Kotlin support
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.flk_version}"
    include "net.fabricmc:fabric-language-kotlin:${project.flk_version}"

    // Cloth Config required by Dehydration
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Dehydration
    modCompileOnly "curse.maven:dehydration-410830:3429561"

    // ItemNBT
    modImplementation "com.redgrapefruit.itemnbt:itemnbt:${project.itemnbt_version}"
    include "com.redgrapefruit.itemnbt:itemnbt:${project.itemnbt_version}"

    // RedMenu
    modImplementation "com.redgrapefruit.redmenu:redmenu:${project.redmenu_version}"
    include "com.redgrapefruit.redmenu:redmenu:${project.redmenu_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

processResources {
    inputs.property "version", project.version

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    // Exclusions for reference textures, templates etc.
    exclude("assets/utopia/textures/ct/")
    exclude("assets/utopia/textures/block/template")

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
